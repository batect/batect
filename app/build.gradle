/*
   Copyright 2017-2020 Charles Korn.

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
*/

plugins {
    id 'com.github.johnrengelman.shadow' version '6.0.0'
    id 'batect-kotlin'
    id 'application'
}

applicationName = 'batect'
archivesBaseName = 'batect'
mainClassName = 'batect.ApplicationKt'

repositories {
    mavenCentral()
    maven { url "https://kotlin.bintray.com/kotlinx" }
    jcenter()
}

dependencies {
    implementation platform("com.squareup.okhttp3:okhttp-bom:4.7.2")

    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.20.0'
    implementation 'org.kodein.di:kodein-di-core-jvm:6.5.5'
    implementation 'org.kodein.di:kodein-di-generic-jvm:6.5.5'
    implementation 'com.squareup.okio:okio:2.6.0'
    implementation 'com.squareup.okhttp3:okhttp'
    implementation 'com.github.jnr:jnr-posix:3.0.57'
    implementation 'com.charleskorn.kaml:kaml:0.17.0'
    implementation 'com.hypirion:io:0.3.1'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.66'

    implementation project(':libs:docker-client')
    implementation project(':libs:git-client')
    implementation project(':libs:logging')
    implementation project(':libs:primitives')
    implementation project(':libs:os')

    testImplementation 'org.araqnid:hamkrest-json:1.1.2'
    testImplementation 'com.google.jimfs:jimfs:1.1'
    testImplementation project(':libs:test-utils')
    testImplementation project(':libs:logging-test-utils')
}

tasks.named("check").configure {
    dependsOn ':libs:check'
}

checkUnitTestLayout {
    ignoreFileNameCheck = fileTree(
        dir: 'src/unitTest/kotlin',
        includes: [
            'batect/cli/commands/FakeDockerConnectivity.kt',
            'batect/testutils/**',
        ]
    )

    ignoreMissingMainFile = fileTree(
        dir: 'src/unitTest/kotlin',
        includes: [
            'batect/execution/model/steps/*.kt',
            'batect/execution/model/events/*.kt',
        ]
    )
}

apply from: 'gradle/journeyTest.gradle'
apply from: 'gradle/performanceTest.gradle'
apply from: 'gradle/profiling.gradle'
apply from: 'gradle/shadow.gradle'
apply from: 'gradle/versionInfo.gradle'
