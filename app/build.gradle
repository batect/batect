/*
   Copyright 2017-2019 Charles Korn.

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
*/

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-serialization:1.3.61"
    }
}

plugins {
    id 'com.diffplug.gradle.spotless'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
}

apply plugin: 'application'
apply plugin: 'kotlinx-serialization'

applicationName = 'batect'
archivesBaseName = 'batect'
mainClassName = 'batect.ApplicationKt'

repositories {
    mavenCentral()
    maven { url "https://kotlin.bintray.com/kotlinx" }
    jcenter()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.14.0"
    implementation 'org.kodein.di:kodein-di-core-jvm:6.4.1'
    implementation 'org.kodein.di:kodein-di-generic-jvm:6.4.1'
    implementation 'com.squareup.okio:okio:2.4.1'
    implementation 'com.squareup.okhttp3:okhttp:3.13.1'
    implementation 'com.github.jnr:jnr-unixsocket:0.24'
    implementation 'com.github.jnr:jnr-posix:3.0.51'
    implementation 'org.apache.commons:commons-compress:1.19'
    implementation 'com.charleskorn.kaml:kaml:0.15.0'

    testImplementation 'org.araqnid:hamkrest-json:1.1.0'
    // We don't use mockito directly, but mockito-kotlin does refer to it, so override it to get the latest version.
    testImplementation 'org.mockito:mockito-core:3.1.0'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
    testImplementation 'com.google.jimfs:jimfs:1.1'
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.13.1'
}

shadowJar {
    classifier = null
}

// See https://github.com/johnrengelman/shadow/issues/389#issuecomment-440431318 for an explanation of this.
tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ["-progressive"]
    }
}

apply from: 'gradle/testCommon.gradle'
apply from: 'gradle/integrationTest.gradle'
apply from: 'gradle/jacoco.gradle'
apply from: 'gradle/journeyTest.gradle'
apply from: 'gradle/performanceTest.gradle'
apply from: 'gradle/profiling.gradle'
apply from: 'gradle/spotless.gradle'
apply from: 'gradle/unitTest.gradle'
apply from: 'gradle/versionInfo.gradle'
