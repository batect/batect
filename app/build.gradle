/*
   Copyright 2017-2018 Charles Korn.

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
*/

buildscript {
    ext.spek_version = '1.2.1'

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-serialization:1.3.10"
    }
}

plugins {
    id 'com.diffplug.gradle.spotless'
    id 'com.github.johnrengelman.shadow' version '4.0.2'
    id 'org.jetbrains.kotlin.jvm' version '1.3.10'
}

apply plugin: 'application'
apply plugin: 'kotlinx-serialization'

applicationName = 'batect'
archivesBaseName = 'batect'
mainClassName = 'batect.ApplicationKt'

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://kotlin.bintray.com/kotlinx" }
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    // We don't use kotlin-reflect directly, but jackson-module-kotlin does refer to it, so
    // override it to match the version of Kotlin used.
    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.3.11'
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.9.1"

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.7'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.7'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.9.7'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.7'
    implementation 'org.kodein.di:kodein-di-core-jvm:6.0.1'
    implementation 'org.kodein.di:kodein-di-generic-jvm:6.0.1'
    implementation 'com.squareup.okhttp3:okhttp:3.12.0'
    implementation 'com.github.jnr:jnr-unixsocket:0.21'
    implementation 'com.github.jnr:jnr-posix:3.0.47'
    implementation 'org.apache.commons:commons-compress:1.18'

    testImplementation 'com.natpryce:hamkrest:1.6.0.0'
    testImplementation 'org.araqnid:hamkrest-json:1.0.3'
    testImplementation 'com.nhaarman:mockito-kotlin-kt1.1:1.6.0'
    testImplementation 'com.google.jimfs:jimfs:1.1'
    testImplementation "org.jetbrains.spek:spek-api:$spek_version"

    testRuntimeOnly "org.jetbrains.spek:spek-junit-platform-engine:$spek_version"
    testRuntimeOnly 'org.junit.platform:junit-platform-engine:1.3.2'
}

tasks.withType(Test) {
    useJUnitPlatform {
        includeEngines 'spek'
    }

    testLogging {
        events "failed"
        events "skipped"
        events "standard_out"
        events "standard_error"

        showExceptions true
        showStackTraces true
        showCauses true
        exceptionFormat "full"
    }
}

shadowJar {
    classifier = null
}

// See https://github.com/johnrengelman/shadow/issues/389#issuecomment-440431318 for an explanation of this.
tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ["-progressive"]
    }
}

apply from: 'gradle/integrationTest.gradle'
apply from: 'gradle/jacoco.gradle'
apply from: 'gradle/journeyTest.gradle'
apply from: 'gradle/performanceTest.gradle'
apply from: 'gradle/profiling.gradle'
apply from: 'gradle/spotless.gradle'
apply from: 'gradle/versionInfo.gradle'
