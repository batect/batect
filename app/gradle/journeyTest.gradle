/*
   Copyright 2017-2020 Charles Korn.

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
*/

sourceSets {
    journeyTest {
        compileClasspath += testCommon.output
        runtimeClasspath += testCommon.output
    }
}

configurations {
    journeyTestImplementation.extendsFrom testImplementation
    journeyTestRuntimeOnly.extendsFrom testRuntimeOnly
}

task journeyTest(type: Test) {
    description 'Runs the journey tests.'
    group 'Verification'
    testClassesDirs = sourceSets.journeyTest.output.classesDirs
    classpath = sourceSets.journeyTest.runtimeClasspath

    dependsOn journeyTestClasses
    dependsOn installShadowDist

    inputs.files installShadowDist

    if (project.properties.containsKey("windowsContainers")) {
        include 'batect/journeytests/windowscontainers/**'
    } else {
        exclude 'batect/journeytests/windowscontainers/**'
    }
}
