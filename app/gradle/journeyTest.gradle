/*
   Copyright 2017-2020 Charles Korn.

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
*/

sourceSets {
    journeyTest {
        compileClasspath += testCommon.output
        runtimeClasspath += testCommon.output
    }
}

configurations {
    journeyTestImplementation.extendsFrom testImplementation
    journeyTestRuntimeOnly.extendsFrom testRuntimeOnly
}

abstract class JourneyTest extends Test {}

tasks.withType(JourneyTest) {
    group 'Verification'
    testClassesDirs = sourceSets.journeyTest.output.classesDirs
    classpath = sourceSets.journeyTest.runtimeClasspath

    dependsOn journeyTestClasses
    dependsOn installShadowDist

    inputs.files installShadowDist
}

def windowsTests = 'batect/journeytests/windowscontainers/**'
def cleanupTests = 'batect/journeytests/cleanup/**'

task journeyTestWindowsContainers(type: JourneyTest) {
    description 'Runs the journey tests for Windows containers.'

    include windowsTests

    onlyIf { project.properties.containsKey("windowsContainers") }
}

task journeyTestLinuxContainersParallelisable(type: JourneyTest) {
    description 'Runs the journey tests for Linux containers that can run in parallel.'

    exclude windowsTests
    exclude cleanupTests

    onlyIf { !project.properties.containsKey("windowsContainers") }

    maxParallelForks = 4
}

task journeyTestLinuxContainersNonParallelisable(type: JourneyTest) {
    description 'Runs the journey tests for Linux containers that cannot run in parallel.'

    include cleanupTests

    onlyIf { !project.properties.containsKey("windowsContainers") }
}

task journeyTestLinuxContainers {
    description 'Runs the journey tests for Linux containers.'
    group 'Verification'

    dependsOn journeyTestLinuxContainersParallelisable
    dependsOn journeyTestLinuxContainersNonParallelisable
}

task journeyTest {
    description 'Runs the journey tests.'
    group 'Verification'

    dependsOn journeyTestWindowsContainers
    dependsOn journeyTestLinuxContainers
}
