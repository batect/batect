/*
   Copyright 2017 Charles Korn.

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
*/

allprojects {
    buildscript {
        repositories {
            mavenCentral()
        }
    }

    repositories {
        mavenCentral()
    }

    version = '0.3'
}

apply from: "$rootDir/gradle/utilities.gradle"

task assembleRelease(type: Copy) {
    from project("app").getTasksByName("shadowJar", false)
    from project("wrapper").getTasksByName("build", false)
    into getBuildDir().toPath().resolve("release")
}

task validateIsReleaseBuild {
    doLast {
        if (!project.hasProperty("releaseBuild")) {
            throw new Exception("The releaseBuild property was not set. This will cause the version information to be incorrect.")
        }
    }
}

task validateVersionMatchesTag {
    doLast {
        def gitTag = runProcess("git", "describe", "--dirty", "--candidates=0")

        if (gitTag.contains("-dirty")) {
            throw new Exception("The working copy is dirty.")
        }

        if (gitTag != project.version) {
            throw new Exception("The tag for this commit is '$gitTag', but the project version is set to '$project.version'.")
        }
    }
}

task validateRelease {
    dependsOn validateIsReleaseBuild
    dependsOn validateVersionMatchesTag
}
