/*
   Copyright 2017-2020 Charles Korn.

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
*/

def outputDir = getBuildDir().toPath().resolve("docs")
def imageName = "squidfunk/mkdocs-material:5.5.12"

tasks.register("buildDocs", Exec) {
    commandLine "docker", "run", "--rm", "-v", "${project.projectDir}:/docs", "--entrypoint", "sh", imageName, "build.sh"
}

tasks.register("copyConfigSchema", Copy) {
    from "${rootProject.rootDir}/tools/schema/configSchema.json"
    into outputDir

    mustRunAfter buildDocs
}

tasks.register("build") {
    description "Build the documentation."
    group "Build"

    dependsOn buildDocs
    dependsOn copyConfigSchema
}

tasks.register("serve", Exec) {
    description "Serve the documentation from a local web server and rebuild it when the source changes."
    group "Build"

    commandLine "docker", "run", "--rm", "-i", "-p", "8000:8000", "-v", "${project.projectDir}:/docs", imageName, "serve", "-a", "0.0.0.0:8000"
}

tasks.register("clean", Delete) {
    delete outputDir
}
